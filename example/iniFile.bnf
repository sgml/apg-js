;*******************************************************************************
;  JavaScript APG, Version 1.0
;  
;  Copyright (C) 2009 Lowell D. Thomas, all rights reserved
;
;  author:  Lowell D. Thomas
;  email:   lowell@coasttocoastresearch.com
;
;  This program is free software: you can redistribute it and/or modify
;  it under the terms of the GNU General Public License as published by
;  the Free Software Foundation, either version 2 of the License, or
;  (at your option) any later version.
;
;  This program is distributed in the hope that it will be useful,
;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;  GNU General Public License for more details.
;
;  You should have received a copy of the GNU General Public License
;  along with this program.  If not, see
;  <http://www.gnu.org/licenses/old-licenses/gpl-2.0.html>
;  or write to the Free Software Foundation, Inc.,
;  51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
;*******************************************************************************
;
IniFile         = *BlankLine *Section
Section         = SectionLine *(BlankLine/ValueLine)
SectionLine     = GoodSectionLine/BadSectionLine
GoodSectionLine = "[" wsp SectionName wsp "]" wsp [comment] LineEnd
BadSectionLine  = "[" *any LineEnd;
ValueLine       = GoodValueLine/BadValueLine
GoodValueLine   = ValueName wsp "=" wsp ValueArray wsp [comment] LineEnd
BadValueLine    = (%d33-90/%d92-126) *any LineEnd
ValueArray      = Value *(wsp "," wsp Value)
SectionName     = 1*(alpha/digit/%d95)
ValueName       = 1*(alpha/digit/%d95)
Value           = DQuotedString/SQuotedString/AlphaDigit
DQuotedString   = %d34 1*(%d32-33/%d35-126) %d34
SQuotedString   = %d39 1*(%d32-38/%d40-126) %d39
AlphaDigit      = 1*(alpha/digit)
BlankLine       = wsp [comment] LineEnd
LineEnd         = %d13.10/%d10/%d13
comment         = ";" *any
wsp             = *(%d32/%d9)
alpha           = %d65-90/%d97-122
digit           = %d48-57
any             = %d32-126/%d9